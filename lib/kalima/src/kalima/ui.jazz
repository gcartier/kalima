;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Kalima UI
;;;


(module kalima.ui jazz


(import (jazz.action)
        (jazz.application)
        (jazz.clipboard)
        (jazz.event)
        (jazz.feedback)
        (jazz.graphic)
        (jazz.handler)
        (jazz.jml)
        (jazz.layout)
        (jazz.locale)
        (jazz.resource)
        (jazz.settings)
        (jazz.text)
        (jazz.tree)
        (jazz.ui)
        (jazz.undoer)
        (jazz.view)
        (jazz.workspace)
        (kalima))


;;;
;;;; Font
;;;


(define Kalima-Font-Sizes
  '(18 24 36 48 72))


(define-font Kalima
  font-name: 'vera-sans
  point-size: 36)


(definition public (get-kalima-point-size)
  (find-setting 'kalima.point-size 36))

(definition public (set-kalima-point-size point-size)
  (set-setting~ (get-settings~ (get-application)) 'kalima.point-size point-size))


;;;
;;;; View
;;;


(class Kalima-View extends Layout-View
  
  
  (slot point-size initialize #f)
  
  
  (method (change-size changer)
    (let ((new-point-size (changer Kalima-Font-Sizes (or point-size (get-kalima-point-size)) cycle?: #f)))
      (when new-point-size
        (set-kalima-point-size new-point-size)
        (set! point-size new-point-size)
        (size-update))))
  
  
  (method (size-update)
    (let ((center (locate 'center))
          (entry (locate 'entry))
          (primary (locate 'primary))
          (secondary (locate 'secondary))
          (tertiary (locate 'tertiary)))
      (let ((font (new Font font-name: 'vera-sans @windows "Times New Roman" point-size: (or point-size (get-kalima-point-size)))))
        (let ((height (font-height~ font)))
          (let ((spacer (fxround (/ (fl height) 10.))))
            (set-height~ entry (+ height spacer height spacer height))
            (set-height~ primary height)
            (set-font~ primary font)
            (set-height~ secondary height)
            (set-font~ secondary font)
            (set-height~ tertiary height)
            (set-font~ tertiary font))))
      (layout-view~ entry)
      (layout-view~ center))))


;;;
;;;; Study
;;;


(class Kalima-Study-View extends Kalima-View implements Dictionary-Listener
  
  
  (method meta override (host-icon)
    {Image-Resource "Pad"})
  
  (method meta override (host-title)
    {Locales english: "Study" french: "Étudier"})
  
  (method meta override (host-position)
    {Point 25 25})
  
  (method meta override (host-size)
    {Dimension 800 500})
  
  
  (slot dictionary initialize #f)
  (slot review     initialize #f)
  (slot right      initialize #f)
  (slot remaining  initialize #f)
  (slot entry      initialize #f)
  (slot ask        initialize #f)
  (slot language   initialize #f)
  (slot restrict   initialize #f)
  (slot stage      initialize #f)
  (slot undoer     initialize #f)
  
  
  (form
    (<install>                                           layout-type: border
      (<Layout-View>                    name: toolbar    size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        (<Border-View>                                   size: {Dimension 140 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
          (<Border-View>                                 border-color: {Color Entry-Border}
            (<Combo-Box>                name: ask        content-change-handler: {Event-Handler :document on-ask})))
        (<Separator-View>                                size: {Dimension 4 0})
        (<Border-View>                                   size: {Dimension 140 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
          (<Border-View>                                 border-color: {Color Entry-Border}
            (<Combo-Box>                name: restrict   content-change-handler: {Event-Handler :document on-restrict})))
        (<Separator>                                     size: {Dimension 14 0})
        (<Push-Tool>                                     size: {Dimension 22 22} tooltip?: #t tooltip-text: "Review" resource: {Image-Resource "Edit"} portfolio: :images action-handler: {Event-Handler :form on-review})
        (<Separator>                                     size: {Dimension 8 0})
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Restart session" resource: {Image-Resource "Refresh"} portfolio: :images action-handler: {Event-Handler :form on-restart})
        (<Separator>                                     location: tail size: {Dimension 20 0})
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Decrease Text" resource: {Image-Resource "FindNext"} portfolio: :images action-handler: {Event-Handler :form on-decrease})
        (<Separator>                                     location: tail size: {Dimension 2 0})
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Increase Text" resource: {Image-Resource "FindPrevious"} portfolio: :images action-handler: {Event-Handler :form on-increase})
        (<Separator>                                     location: tail size: {Dimension 16 0})
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Edit" resource: {Image-Resource "Script"} portfolio: :images action-handler: {Event-Handler :form on-edit})
        (<Separator>                                     location: tail size: {Dimension 2 0}))
      (<Border-View>                                     location: center border-type: edge style: entry
        (<Splitter-View>                                 background: {Color White} orientation: horz mode: absolute division: 150
          (<Scroller-View>                               location: first style: document hscroll?: #t vscroll?: #t
            (<!>                        name: content    layout-type: fill
              (<Tree-View>              name: subjects   multiple-selection?: #t portfolio: :images
                (<Tree-Node-Column>                      title: "Subject" display-images?: #f toplevel-controls?: #f width: 419))))
          (<Layout-View>                                 location: second layout-type: border
            (<Layout-View>              name: center     location: center layout-type: center
              (<Layout-View>            name: entry      layout-type: border size: {Dimension 4000 110}
                (<Label-View>           name: primary    location: north size: {Dimension 4000 50} justification: center font: {Font Kalima} mouse-down-handler: {Event-Handler :form on-entry})
                (<Label-View>           name: secondary  location: center size: {Dimension 4000 50} justification: center font: {Font Kalima} mouse-down-handler: {Event-Handler :form on-entry})
                (<Label-View>           name: tertiary   location: south size: {Dimension 4000 50} justification: center font: {Font Kalima} mouse-down-handler: {Event-Handler :form on-entry})))
            (<Layout-View>                               size: {Dimension 200 20} location: south layout-type: fill layout-insets: {Rect 4 0 4 0}
              (<Layout-View>                             layout-type: border
                (<Label-View>           name: note       size: {Dimension 400 20} location: west)
                (<Label-View>           name: statistics size: {Dimension 400 20} location: east justification: tail))))))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set! undoer (new Undoer))
    (size-update)
    (random-seed)
    (setup)
    (add-listener~ dictionary self))
  
  
  (method override (destroy)
    (remove-listener~ dictionary self)
    (nextmethod))
  
  
  ;;;
  ;;;; Setup
  ;;;
  
  
  (method (setup)
    (define (setup-session)
      (define (setup subject)
        (or (find-theme~ dictionary subject)
            (let ((theme (new Kalima-Theme dictionary subject '())))
              (add-theme~ dictionary theme)
              theme)))
      
      (set! review (setup "<Review>"))
      (set! right (setup "<Right>"))
      (set! remaining (setup "<Remaining>")))
    
    (define (setup-ask)
      (let ((combo (locate 'ask))
            (random {Locales english: "Random" french: "Aléatoire"})
            (answer {Locales english: "Answer" french: "Réponse"}))
        (set-choices~ combo `((primary ,(get-primary~ dictionary)) (secondary ,(get-secondary~ dictionary)) (tertiary ,(get-tertiary~ dictionary)) (:random ,random) (:answer ,answer)))
        (set-value~ combo 'primary)))
    
    (define (setup-restrict)
      (set! restrict 'any)
      (let ((combo (locate 'restrict))
            (any {Locales english: "Any" french: "Tous"}))
        (set-choices~ combo `((any ,any) (secondary ,(get-secondary~ dictionary)) (tertiary ,(get-tertiary~ dictionary))))
        (set-value~ combo 'any)))
    
    (set! dictionary (get-current-dictionary))
    (setup-session)
    (set! ask 'primary)
    (setup-ask)
    (setup-restrict)
    (setup-subjects)
    (setup-entries #f))
  
  
  (method (setup-subjects)
    (define (collect-subjects)
      (let ((queue (new-queue)))
        (for-each (lambda (theme)
                    (let ((subject (get-subject~ theme)))
                      (enqueue queue (cons subject theme))))
                  (sort-themes~ dictionary))
        (queue-list queue)))
    
    (let ((tree (locate 'subjects)))
      (remove-every-row~ tree)
      (with-update-locked~ tree
        (lambda ()
          (define (check-box title checked?)
            (let ((handler (new Event-Handler execute: (lambda (evt) (subjects-update)))))
              (new Check-Box title: title checked?: checked? content-change-handler: handler)))
          
          (add-row~ tree children: (list (check-box "All" #f)) user-data: #f)
          (for-each (lambda (info)
                      (bind (subject . theme) info
                        (let ((title (if (starts-with? subject "<") (format "{a} ({a})" subject (length (get-entries~ theme))) subject)))
                          (add-row~ tree children: (list (check-box title #f)) user-data: theme))))
                    (collect-subjects))))))
  
  
  (method (subjects-update)
    (define (collect-entries)
      (let ((tree (locate 'subjects))
            (entries (new-queue))
            (getter (case ask
                      ((primary) get-primary~)
                      ((secondary) get-secondary~)
                      ((tertiary) get-tertiary~)
                      (else #f)))
            (restrict (case restrict
                        ((any) #f)
                        ((secondary) get-secondary~)
                        ((tertiary) get-tertiary~))))
        (for-each-visible-row~ tree
          (lambda (row line)
            (let ((checkbox (first-child~ row)))
              (when (get-checked?~ checkbox)
                (let ((theme (get-user-data~ row)))
                  (for-each (lambda (entry)
                              (unless (memq? entry (queue-list entries))
                                (when (or (not getter) (not (empty-string? (getter entry))))
                                  (when (or (not restrict) (not (empty-string? (restrict entry))))
                                    (enqueue entries entry)))))
                            (if theme
                                (get-entries~ theme)
                              (collect-entries~ dictionary))))))))
        (queue-list entries)))
    
    (setup-entries (collect-entries)))
  
  
  (method (setup-entries lst)
    (reset~ undoer)
    (set! stage 'question)
    (when lst
      (replace-entries~ right '())
      (replace-entries~ remaining lst))
    (ask-word)
    (delay-event
      (lambda ()
        (update-focus-actions~ (get-application)))))
  
  
  ;;;
  ;;;; Edition
  ;;;
  
  
  (method override (can-undo?)
    (can-undo?~ undoer))
  
  
  (method override (can-redo?)
    (can-redo?~ undoer))
  
  
  (method override (undo)
    (undo~ undoer))
  
  
  (method override (redo)
    (redo~ undoer))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (get-tab-stops)
    (list
      self))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions)
    (cons (find-actions 'kalima-study)
          (nextmethod)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-ask evt)
    (set! ask (get-value~ (get-sender~ evt)))
    (subjects-update)
    (ask-word))
  
  
  (method (on-restrict evt)
    (set! restrict (get-value~ (get-sender~ evt)))
    (subjects-update)
    (ask-word))
  
  
  (method (on-review evt)
    (setup-entries (get-entries~ review)))
  
  
  (method (on-restart evt)
    (subjects-update))
  
  
  (method (on-decrease evt)
    (change-size previous-element))
  
  
  (method (on-increase evt)
    (change-size next-element))
  
  
  (method (on-edit evt)
    (if (not entry)
        (bell)
      (edit-entry entry (displayed))))
  
  
  (method (on-entry evt)
    (acquire-focus~ self))
  
  
  (method override (key-press evt)
    (case (get-key~ evt)
      ((#\space)
       (if (eq? ask :answer)
           (review-answer)
         (case stage
           ((question)
            (show-answer))
           ((answer)
            (review-answer)))))))
  
  
  (method override (return-press evt)
    (if (eq? ask :answer)
        (right-answer)
      (case stage
        ((question)
         (show-answer))
        ((answer)
         (right-answer)))))
  
  
  (method override (tab-press evt)
    (right-answer))
  
  
  (method override (entry-change dictionary changed-entry)
    (when (eq? changed-entry entry)
      (update)))
  
  
  ;;;
  ;;;; Management
  ;;;
  
  
  (method (ask-word)
    (ask-entry (random-entry~ remaining)))
  
  
  (method (ask-entry new-entry)
    (if (not new-entry)
        (set! entry #f)
      (set! stage 'question)
      (set! language (case ask
                       ((:random) (random-element (let ((languages '()))
                                                    (define (add language getter)
                                                      (when (not (empty-string? (getter new-entry)))
                                                        (set! languages (cons language languages))))
                                                    
                                                    (add 'primary get-primary~)
                                                    (add 'secondary get-secondary~)
                                                    (add 'tertiary get-tertiary~)
                                                    languages)))
                       ((:answer) 'secondary)
                       (else ask)))
      (set! entry new-entry))
    (update))
  
  
  (method (show-answer)
    (when entry
      (set! stage 'answer)
      (update)))
  
  
  (method (right-answer)
    (when entry
      (register-undo)
      (remove-entry~ review entry)
      (remove-entry~ remaining entry)
      (unless (has-entry?~ right entry)
        (add-entry~ right entry))
      (set-modified?~ dictionary #t)
      (ask-word)
      (delay-event
        (lambda ()
          (update-focus-actions~ (get-application))))))
  
  
  (method (review-answer)
    (when entry
      (register-undo)
      (remove-entry~ remaining entry)
      (unless (has-entry?~ review entry)
        (add-entry~ review entry))
      (set-modified?~ dictionary #t)
      (ask-word)
      (delay-event
        (lambda ()
          (update-focus-actions~ (get-application))))))
  
  
  (method (register-undo)
    (register-undo~ undoer
      (let ((review (get-entries~ review))
            (right (get-entries~ right))
            (remaining (get-entries~ remaining))
            (entry entry))
        (lambda ()
          (register-undo)
          (replace-entries~ review~self review)
          (replace-entries~ right~self right)
          (replace-entries~ remaining~self remaining)
          (ask-entry entry)
          (delay-event
            (lambda ()
              (update-focus-actions~ (get-application))))))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update)
    (if (not entry)
        (begin
          (display "" "" "")
          (display-note "" #f))
      (let ((primary (get-primary~ entry))
            (secondary (get-secondary~ entry))
            (tertiary (get-tertiary~ entry))
            (note (get-note~ entry))
            (themes (present-entry-themes~ dictionary entry)))
        (if (or (eq? ask :answer)
                (eq? stage 'answer))
            (display primary secondary tertiary)
          (case language
            ((primary) (display primary "" ""))
            ((secondary) (display "" secondary ""))
            ((tertiary) (display "" "" tertiary))))
        (display-note note themes)))
    (statistics))
  
  
  (method (displayed)
    (case stage
      ((question)
       language)
      ((answer)
       (case language
         ((primary) 'secondary)
         ((secondary) 'primary)))))
  
  
  (method (display primary secondary tertiary)
    (let ((label (locate 'primary)))
      (set-color~ label {Color Black})
      (set-title~ label primary))
    (let ((label (locate 'secondary)))
      (set-color~ label {Color red: .131 green: .131 blue: .619})
      (set-title~ label secondary))
    (let ((label (locate 'tertiary)))
      (set-color~ label {Color red: .103 green: .502 blue: .151})
      (set-title~ label tertiary)))
  
  
  (method (display-note text themes)
    (let ((label (locate 'note)))
      (if (empty-string? text)
          (set-title~ label (format "{?{a}~}" themes))
        (set-title~ label (format "{a}{? : {a}~}" text themes)))))
  
  
  (method (statistics)
    (let ((label (locate 'statistics)))
      (set-title~ label (format "Review: {a}, right: {a}, remaining: {a}" (count-entries~ review) (count-entries~ right) (count-entries~ remaining))))))


;;;
;;;; Study-Actions
;;;


(class Kalima-Study-Actions extends Actions


  (form
    (<install>
      (<Action-Item> name: review   action-handler: {Event-Handler :focus-guest on-review} shortcut: {Shortcut :alt #\R})
      (<Action-Item> name: restart  action-handler: {Event-Handler :focus-guest on-restart} shortcut: {Shortcut :alt #\L})
      (<Action-Item> name: increase action-handler: {Event-Handler :focus-guest on-increase} shortcut: {Shortcut :control #\+})
      (<Action-Item> name: decrease action-handler: {Event-Handler :focus-guest on-decrease} shortcut: {Shortcut :control #\-})
      (<Action-Item> name: edit     action-handler: {Event-Handler :focus-guest on-edit} shortcut: {Shortcut :alt #\E} alternate-shortcut: {Shortcut F2}))))


;;;
;;;; Browse
;;;


(class Kalima-Browse-View extends Kalima-View implements Dictionary-Listener
  
  
  (method meta override (host-icon)
    {Image-Resource "Doc"})
  
  (method meta override (host-title)
    {Locales english: "Browse" french: "Consulter"})
  
  (method meta override (host-position)
    {Point 834 25})
  
  (method meta override (host-size)
    {Dimension 800 697})
  
  
  (slot dictionary initialize #f)
  (slot themes     initialize #f)
  (slot entries    initialize #f)
  (slot entry      initialize #f)
  (slot theme      initialize #f)
  (slot language   initialize #f)
  
  
  (form
    (<install>                                           layout-type: border
      (<Layout-View>                    name: toolbar    size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        (<Border-View>                                   size: {Dimension 140 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
          (<Border-View>                                 border-color: {Color Entry-Border}
            (<Combo-Box>                name: theme      content-change-handler: {Event-Handler :document on-theme})))
        (<Separator-View>                                size: {Dimension 4 0})
        (<Border-View>                                   size: {Dimension 140 22} border-insets: {Rect 2 2 2 1} border-color: {Color Medium}
          (<Border-View>                                 border-color: {Color Entry-Border}
            (<Combo-Box>                name: language   content-change-handler: {Event-Handler :document on-language})))
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Decrease Text" resource: {Image-Resource "FindNext"} portfolio: :images action-handler: {Event-Handler :form on-decrease})
        (<Separator>                                     location: tail size: {Dimension 2 0})
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Increase Text" resource: {Image-Resource "FindPrevious"} portfolio: :images action-handler: {Event-Handler :form on-increase})
        (<Separator>                                     location: tail size: {Dimension 16 0})
        (<Push-Tool>                                     location: tail size: {Dimension 22 22} tooltip?: #t tooltip-text: "Edit" resource: {Image-Resource "Script"} portfolio: :images action-handler: {Event-Handler :form on-edit})
        (<Separator>                                     location: tail size: {Dimension 2 0}))
      (<Border-View>                                     location: center border-type: edge style: entry
        (<Splitter-View>                                 background: {Color White} orientation: horz mode: absolute division: 175
          (<Scroller-View>                               location: first style: document hscroll?: #t vscroll?: #t
            (<!>                        name: content    layout-type: fill
              (<Tree-View>              name: entries    portfolio: :images selection-handler: {Event-Handler :form on-selection-change}
                (<Tree-Node-Column>                      title: "Word" display-images?: #f toplevel-controls?: #f width: 419))))
          (<Layout-View>                                 location: second layout-type: border
            (<Layout-View>              name: center     location: center layout-type: center
              (<Layout-View>            name: entry      layout-type: border size: {Dimension 4000 160}
                (<Label-View>           name: primary    location: north size: {Dimension 4000 50} justification: center font: {Font Kalima})
                (<Label-View>           name: secondary  location: center size: {Dimension 4000 50} justification: center font: {Font Kalima})
                (<Label-View>           name: tertiary   location: south size: {Dimension 4000 50} justification: center font: {Font Kalima})))
            (<Layout-View>                               size: {Dimension 400 20} location: south layout-type: fill layout-insets: {Rect 4 0 0 0}
              (<Label-View>             name: note)))))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (size-update)
    (setup)
    (add-listener~ dictionary self))
  
  
  (method override (destroy)
    (remove-listener~ dictionary self)
    (nextmethod))
  
  
  (method override (focus-default)
    (focus-entries))
  
  
  (method (focus-entries)
    (let ((tree (locate 'entries)))
      (acquire-focus~ tree)
      (select-first-row~ tree)))
  
  
  ;;;
  ;;;; Setup
  ;;;
  
  
  (method (setup)
    (define (setup-theme)
      (let ((combo (locate 'theme)))
        (set-choices~ combo `((#f "All") ,@(map (lambda (theme) (list theme (get-subject~ theme))) (sort-themes~ dictionary))))
        (set-value~ combo #f)))
    
    (define (setup-language)
      (set! language 'primary)
      (let ((combo (locate 'language)))
        (set-choices~ combo `((primary ,(get-primary~ dictionary)) (secondary ,(get-secondary~ dictionary)) (tertiary ,(get-tertiary~ dictionary))))
        (set-value~ combo 'primary)))
    
    (set! dictionary (get-current-dictionary))
    (setup-entries)
    (setup-theme)
    (setup-language)
    (setup-words))
  
  
  (method (setup-entries)
    (set! entries (if theme
                      (get-entries~ theme)
                    (collect-entries~ dictionary))))
  
  
  (method (setup-words)
    (let ((tree (locate 'entries))
          (getter (case language
                    ((primary) get-primary~)
                    ((secondary) get-secondary~)
                    ((tertiary) get-tertiary~))))
      (remove-every-row~ tree)
      (with-update-locked~ tree
        (lambda ()
          (for-each (lambda (entry)
                      (let ((title (getter entry)))
                        (unless (empty-string? title)
                          (add-row~ tree children: (list (new Tree-Node title: title)) user-data: entry))))
                    (di-sort-ascending entries key: getter))))))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (get-tab-stops)
    (list
      (locate 'entries)))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions)
    (cons (find-actions 'kalima-browse)
          (nextmethod)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-decrease evt)
    (change-size previous-element))
  
  
  (method (on-increase evt)
    (change-size next-element))
  
  
  (method (on-edit evt)
    (if (not entry)
        (bell)
      (edit-entry entry 'primary)))
  
  
  (method (on-theme evt)
    (set! theme (get-value~ (get-sender~ evt)))
    (setup-entries)
    (setup-words)
    (display "" "" "")
    (display-note "" #f)
    (focus-entries))
  
  
  (method (on-language evt)
    (set! language (get-value~ (get-sender~ evt)))
    (setup-words)
    (display "" "" "")
    (display-note "" #f)
    (focus-entries))
  
  
  (method (on-selection-change evt)
    (let ((tree (get-sender~ evt)))
      (set! entry (get-single-selected-data~ tree))
      (update)))
  
  
  (method override (entry-change dictionary changed-entry)
    (when (eq? changed-entry entry)
      (update)))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update)
    (if (not entry)
        (begin
          (display "" "" "")
          (display-note "" #f))
      (let ((primary (get-primary~ entry))
            (secondary (get-secondary~ entry))
            (tertiary (get-tertiary~ entry))
            (note (get-note~ entry))
            (themes (present-entry-themes~ dictionary entry)))
        (display primary secondary tertiary)
        (display-note note themes))))
  
  
  (method (display primary secondary tertiary)
    (let ((label (locate 'primary)))
      (set-color~ label {Color Black})
      (set-title~ label primary))
    (let ((label (locate 'secondary)))
      (set-color~ label {Color red: .131 green: .131 blue: .619})
      (set-title~ label secondary))
    (let ((label (locate 'tertiary)))
      (set-color~ label {Color red: .103 green: .502 blue: .151})
      (set-title~ label tertiary)))
  
  
  (method (display-note text themes)
    (let ((label (locate 'note)))
      (if (empty-string? text)
          (set-title~ label (format "{?{a}~}" themes))
        (set-title~ label (format "{a}{? : {a}~}" text themes))))))


;;;
;;;; Browse-Actions
;;;


(class Kalima-Browse-Actions extends Actions


  (form
    (<install>
      (<Action-Item> name: increase action-handler: {Event-Handler :focus-guest on-increase} shortcut: {Shortcut :control #\+})
      (<Action-Item> name: decrease action-handler: {Event-Handler :focus-guest on-decrease} shortcut: {Shortcut :control #\-})
      (<Action-Item> name: edit     action-handler: {Event-Handler :focus-guest on-edit} shortcut: {Shortcut :alt #\E} alternate-shortcut: {Shortcut F2}))))


;;;
;;;; Edit
;;;


(class Kalima-Edit-View extends Layout-View
  
  
  (method meta override (host-icon)
    {Image-Resource "WorkspaceText"})
  
  (method meta override (host-title)
    {Locales english: "Edit" french: "Éditer"})
  
  (method meta override (host-position)
    {Point 25 534})
  
  (method meta override (host-size)
    {Dimension 800 500})
  
  
  (slot dictionary initialize #f)
  (slot selected   initialize #f)
  
  
  (form
    (<install>                                           layout-type: border
      (<Layout-View>                    name: toolbar    size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        (<Push-Tool>                                     size: {Dimension 22 22} tooltip?: #t tooltip-text: "Add Entry" resource: {Image-Resource "Add"} portfolio: :images action-handler: {Event-Handler :form on-add})
        (<Separator>                                     size: {Dimension 4 0})
        (<Push-Tool>                                     size: {Dimension 22 22} tooltip?: #t tooltip-text: "Remove Entry" resource: {Image-Resource "Remove"} portfolio: :images action-handler: {Event-Handler :form on-remove}))
      (<Border-View>                                     location: center border-type: edge style: entry
        (<Splitter-View>                                 background: {Color White} orientation: vert mode: absolute division: -183
          (<Scroller-View>                               location: first style: document hscroll?: #t vscroll?: #t
            (<!>                         name: content   layout-type: fill
              (<Tree-Header>                             style: document
                (<!>                     name: content
                  (<Tree-View>           name: entries   filled-column: note portfolio: :images multiple-selection?: #t selection-handler: {Event-Handler :form on-selection-change}
                    (<Tree-Node-Column>  name: primary   title: "" display-images?: #f toplevel-controls?: #f width: 175)
                    (<Tree-Label-Column> name: secondary title: "" width: 175)
                    (<Tree-Label-Column> name: tertiary  title: "" width: 225)
                    (<Tree-Label-Column> name: note      title: "Note")
                    (<Tree-Label-Column> name: themes    title: "Themes" width: 150))))))
          (<Layout-View>                                 location: second layout-type: center
            (<Kalima-Entry-View>         name: entry     size: {Dimension 787 110} location: center))))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (setup))
  
  
  (method override (focus-default)
    (let ((tree (locate 'entries)))
      (acquire-focus~ tree)
      (select-first-row~ tree)))
  
  
  ;;;
  ;;;; Setup
  ;;;
  
  
  (method (setup)
    (set! dictionary (get-current-dictionary))
    (set-dictionary~ (locate 'entry) dictionary)
    (setup-entries))
  
  
  (method (setup-entries)
    (let ((entries (collect-entries~ dictionary))
          (tree (locate 'entries)))
      (remove-every-row~ tree)
      (with-update-locked~ tree
        (lambda ()
          (for-each (lambda (entry)
                      (let ((primary (get-primary~ entry))
                            (secondary (get-secondary~ entry))
                            (tertiary (get-tertiary~ entry))
                            (note (get-note~ entry))
                            (themes (or (present-entry-themes~ dictionary entry) "")))
                        (add-row~ tree children: (list (new Tree-Node title: primary) (new Tree-Label title: secondary) (new Tree-Label title: tertiary) (new Tree-Label title: note) (new Tree-Label title: themes)) user-data: entry)))
                    (di-sort-ascending entries key: get-primary~))))
      (let ((primary (get-primary~ dictionary))
            (secondary (get-secondary~ dictionary))
            (tertiary (get-tertiary~ dictionary)))
        (set-title~ (locate 'primary) primary)
        (set-title~ (locate 'secondary) secondary)
        (set-title~ (locate 'tertiary) tertiary)
        (let ((entry (locate 'entry)))
          (set-title~ (locate~ entry 'primary-label) (format "{a}:" primary))
          (set-title~ (locate~ entry 'secondary-label) (format "{a}:" secondary))
          (set-title~ (locate~ entry 'tertiary-label) (format "{a}:" tertiary))))))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (get-tab-stops)
    (list
      (locate 'entries)
      (locate '(entry primary))
      (locate '(entry secondary))
      (locate '(entry tertiary))
      (locate '(entry note))))
  
  
  ;;;
  ;;;; Actions
  ;;;
  

  (method override (guest-actions)
    (cons (find-actions 'kalima-edit)
          (nextmethod)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-add evt)
    (let ((tree (locate 'entries))
          (entry (new Kalima-Entry "" "" "" "")))
      (add-entry~ dictionary entry)
      (setup-entries)
      (select-user-data-row~ tree entry)
      (acquire-focus~ (locate '(entry primary)))))
  
  
  (method (on-remove evt)
    (let ((tree (locate 'entries)))
      (let ((rows (get-selected-rows~ tree)))
        (for-each (lambda (row)
                    (let ((entry (get-user-data~ row))
                          (count (get-visible-count~ tree)))
                      (let ((next (and (> count 1) (or (get-next~ row) (get-row~ tree (- count 2))))))
                        (remove-entry~ dictionary entry)
                        (setup-entries)
                        (when next
                          (select-user-data-row~ tree (get-user-data~ next))))))
                  rows))))
  
  
  (method (on-selection-change evt)
    (let ((tree (get-sender~ evt)))
      (let ((entries (get-selected-data~ tree)))
        (select-entries entries))))
  
  
  (method (select-entries entries (field #f))
    (let ((entry-view (locate 'entry)))
      (set-entries~ entry-view entries)
      (set! selected entries)
      (when field
        (let ((text-view (locate~ entry-view field)))
          (select-all~ text-view)
          (acquire-focus~ text-view)))))
  
  
  (method (text-change field content)
    (for-each (lambda (entry)
                (update-entry~ dictionary entry field content))
              selected)))


(definition (edit-entry entry field)
  (let ((frame (find-frame-host Kalima-Edit-View)))
    (let ((editor (if frame
                      (begin
                        (focus-host~ frame)
                        (get-guest~ frame))
                    (get-guest~ (new-frame Kalima-Edit-View)))))
      (select-entries~ editor (list entry) field))))


;;;
;;;; Edit-Actions
;;;


(class Kalima-Edit-Actions extends Actions


  (form
    (<install>
      (<Action-Item> name: add action-handler: {Event-Handler :focus-guest on-add} shortcut: {Shortcut :alt #\A}))))


;;;
;;;; Entry
;;;


(class Kalima-Entry-View extends View
  
  
  (slot dictionary initialize #f accessors generate)
  (slot entries    initialize #f getter generate)
  
  
  (form
    (<install>
      (<Label-View>                   name: primary-label             position: {Point 0 2} size: {Dimension 50 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 50 0} size: {Dimension 300 19} border-type: edge style: entry
        (<Scroller-View>                                              hscroll?: #f vscroll?: #f
          (<!>                        name: content                   layout-type: fill
            (<Kalima-Entry-Text-View> name: primary))))
      (<Label-View>                   name: secondary-label           position: {Point 0 32} size: {Dimension 50 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 50 30} size: {Dimension 300 19} border-type: edge style: entry
        (<Scroller-View>                                              hscroll?: #f vscroll?: #f
          (<!>                        name: content                   layout-type: fill
            (<Kalima-Entry-Text-View> name: secondary))))
      (<Label-View>                   name: tertiary-label            position: {Point 0 62} size: {Dimension 50 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 50 60} size: {Dimension 300 19} border-type: edge style: entry
        (<Scroller-View>                                              hscroll?: #f vscroll?: #f
          (<!>                        name: content                   layout-type: fill
            (<Kalima-Entry-Text-View> name: tertiary))))
      (<Label-View>                                   title: "Note:"  position: {Point 0 92} size: {Dimension 50 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 50 90} size: {Dimension 300 19} border-type: edge style: entry
        (<Scroller-View>                                              hscroll?: #f vscroll?: #f
          (<!>                        name: content                   layout-type: fill
            (<Kalima-Entry-Text-View> name: note))))
      (<Label-View>                                   title: "Theme:" position: {Point 380 2} size: {Dimension 40 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 426 0} size: {Dimension 140 20} border-color: {Color Entry-Border}
        (<Combo-Box>                  name: theme1                    content-change-handler: {Event-Handler :form on-theme}))
      (<Label-View>                                   title: "Theme:" position: {Point 380 32} size: {Dimension 40 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 426 30} size: {Dimension 140 20} border-color: {Color Entry-Border}
        (<Combo-Box>                  name: theme2                    content-change-handler: {Event-Handler :form on-theme}))
      (<Label-View>                                   title: "Theme:" position: {Point 380 62} size: {Dimension 40 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 426 60} size: {Dimension 140 20} border-color: {Color Entry-Border}
        (<Combo-Box>                  name: theme3                    content-change-handler: {Event-Handler :form on-theme}))
      (<Label-View>                                   title: "Theme:" position: {Point 597 2} size: {Dimension 40 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 643 0} size: {Dimension 140 20} border-color: {Color Entry-Border}
        (<Combo-Box>                  name: theme4                    content-change-handler: {Event-Handler :form on-theme}))
      (<Label-View>                                   title: "Theme:" position: {Point 597 32} size: {Dimension 40 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 643 30} size: {Dimension 140 20} border-color: {Color Entry-Border}
        (<Combo-Box>                  name: theme5                    content-change-handler: {Event-Handler :form on-theme}))
      (<Label-View>                                   title: "Theme:" position: {Point 597 62} size: {Dimension 40 16} font: {Font Label})
      (<Border-View>                                                  position: {Point 643 60} size: {Dimension 140 20} border-color: {Color Entry-Border}
        (<Combo-Box>                  name: theme6                    content-change-handler: {Event-Handler :form on-theme}))))
  
  
  (method (set-entries entries)
    (define (unique objects)
      (let ((uniques (remove-duplicates objects test: equal?)))
        (and (= 1 (length uniques))
             (car uniques))))
    
    (define (unique-theme themes-list index)
      (unique (map (lambda (themes)
                     (and (>= (length themes) index) (element themes (- index 1))))
                   themes-list)))
    
    (define (setup-theme name theme)
      (let ((combo (locate name)))
        (set-choices~ combo (cons (list #f "<None>") (map (lambda (theme) (list theme (get-subject~ theme))) (sort-themes~ dictionary))))
        (set-value~ combo theme)))
    
    (set! entries~self entries)
    (let ((entry (and (= 1 (length entries)) (car entries))))
      (let ((primary (unique (map get-primary~ entries)))
            (secondary (unique (map get-secondary~ entries)))
            (tertiary (unique (map get-tertiary~ entries)))
            (note (unique (map get-note~ entries)))
            (themes-list (map (lambda (entry)
                                (collect-themes~ dictionary entry))
                              entries)))
        (set-string-content~ (locate 'primary) (or primary ""))
        (set-string-content~ (locate 'secondary) (or secondary ""))
        (set-string-content~ (locate 'tertiary) (or tertiary ""))
        (set-string-content~ (locate 'note) (or note ""))
        (setup-theme 'theme1 (unique-theme themes-list 1))
        (setup-theme 'theme2 (unique-theme themes-list 2))
        (setup-theme 'theme3 (unique-theme themes-list 3))
        (setup-theme 'theme4 (unique-theme themes-list 4))
        (setup-theme 'theme5 (unique-theme themes-list 5))
        (setup-theme 'theme6 (unique-theme themes-list 6)))))
  
  
  (method (on-theme evt)
    (define (collect-themes)
      (remove-duplicates (collect get-value~ (list (locate 'theme1) (locate 'theme2) (locate 'theme3)
                                                   (locate 'theme4) (locate 'theme5) (locate 'theme6)))))
    
    (let ((themes (collect-themes)))
      (for-each (lambda (entry)
                  (update-themes~ dictionary entry themes))
                entries))))


;;;
;;;; Entry-Text
;;;


(class Kalima-Entry-Text-View extends Plain-Text-View
  
  
  (form
    (<install> accepts-returns?: #f show-unfocused-selection?: #f return-press-handler: {Event-Handler :form on-return-press}))
  
  
  (method override (new-model)
    (new Text-Model base-style: {Text-Style Text-User} left-padding: 2 top-padding: 2))
  
  
  (method override (content-change origin)
    (nextmethod origin)
    (let ((editor (find-ascendant Kalima-Edit-View)))
      (text-change~ editor name (get-string-content))))
  
  
  (method override (on-return-press evt)
    (close~ (get-host))))


;;;
;;;; Keyboard
;;;


(class Kalima-Keyboard extends Layout-View
  
  
  (method meta override (host-icon)
    {Image-Resource "Build"})
  
  (method meta override (host-title)
    {Locales english: "Keyboard" french: "Clavier"})
  
  (method meta override (host-position)
    {Point 834 728})
  
  (method meta override (host-size)
    {Dimension 800 306})
  
  
  (form
    (<install>                                              size: {Dimension 420 639} layout-type: border
      (<Layout-View>                   name: toolbar        size: {Dimension 300 33} location: north layout-type: flow layout-insets: {Rect 0 6 0 0}
        (<Push-Tool>                                        enabled?: #f size: {Dimension 22 22} tooltip?: #t tooltip-text: "Add Shortcut" resource: {Image-Resource "Add"} portfolio: :images action-handler: {Event-Handler :form on-add})
        (<Separator>                                        size: {Dimension 4 0})
        (<Push-Tool>                                        enabled?: #f size: {Dimension 22 22} tooltip?: #t tooltip-text: "Remove Shortcut" resource: {Image-Resource "Remove"} portfolio: :images action-handler: {Event-Handler :form on-remove}))
      (<Border-View>                   name: actions-border location: center border-type: edge style: entry
        (<Scroller-View>                                    style: document hscroll?: #t vscroll?: #t
          (<content!>                                       layout-type: fill
            (<Tree-Header>                                  style: document
              (<content!>
                (<Tree-View>           name: actions        portfolio: :images filled-column: text double-click-handler: {Event-Handler :form on-double-click}
                  (<Tree-Node-Column>  name: text           title: "Text" width: 235 display-images?: #f toplevel-controls?: #f)
                  (<Tree-Label-Column> name: shortcut       title: "Shortcut" width: 115)))))))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (setup))
  
  
  (method override (focus-default)
    (let ((tree (locate 'actions)))
      (acquire-focus~ tree)))
  
  
  (method (setup)
    (define (collect-actions)
      (collect-if (lambda (action)
                    (let ((handler (get-action-handler~ action)))
                      (and (is? handler Event-Handler)
                           (eq? (get-method-name~ handler) 'on-insert))))
                  (get-children~ (find-actions 'kalima))))
    
    (let ((actions (collect-actions))
          (tree (locate 'actions)))
      (remove-every-row~ tree)
      (with-update-locked~ tree
        (lambda ()
          (for-each (lambda (action)
                      (let ((text (get-property~ (get-action-handler~ action) text:))
                            (shortcut (present-shortcut~ (get-shortcut~ action))))
                        (add-row~ tree children: (list (new Tree-Node title: text) (new Tree-Label title: shortcut)) user-data: action)))
                    actions)))))
  
  
  (method (on-add evt)
    )
  
  
  (method (on-remove evt)
    )
  
  
  (method (on-double-click evt)
    (let ((tree (get-sender~ evt)))
      (let ((action (get-single-selected-data~ tree)))
        (when action
          (let ((text (get-property~ (get-action-handler~ action) text:)))
            (set-clipboard-text text)
            (user-message "Clipboard set to {a}" text))))))))
